;**************************************************************
;	PIO-Treiber, Version 10.06.87
; Statusabfrage ueber Interrupt
;**************************************************************

cdpioi	equ	dumi		;keine Zeicheneingabe unterstuetzt

; Einzelzeichenausgabe (A)

cdpioo:	ld	c,(ix+ltpsd)	;datenport
	out	(c),a
	ret

	
; Statusabfrage

cdpios:	ld	a,(ix+ltpst)	;Status
	bit	1,a		;senderseitig blockiert?
	jr	nz,cdpsr	;ja, frei rueckmelden
	or	a		;initialisiert?
	call	z,cdpini	;nein
	ld	a,(ix+ltpdc)	;zuletzt empf. Status

; Bedeutung der Statusbits PIO-Drucker:
;--------------------------------------
; Bit 0	Kommandofehler
; Bit 1	Hardwarefehler
; Bit 2	Papierende
; Bit 3	Farbbandende
; Bit 4	Rotdruck
; Bit 5	Druckertyp, 0=1152, 1=1157
; Bit 6	Druckbreite, 0=132, 1=210
; Bit 7	Zeichenabstand, 0=variabel, 1=fest

	and	7		;Fehler?
	cpl			;   z-Flag umkehren
	jr	nz,cdpsr	;nein, frei
	ld	c,(ix+ltpsd)
	out	(c),a		;NOP an Drucker (neuer Status)
cdpsr:
cdpsr1:	ld	a,0ffh
	ret	nz		;senderseitig frei
	inc	a		;a:=00, ret z
	ret			;senderseitig besetzt

; Status-Interrupt-Routine
;-------------------------

cdpsti:	ld	(intsp),sp
	ld	sp,intstk
	push	af
	push	bc
	push	ix
	ld	ix,0
cdpcpa	equ	$-2		;Adresse der Steuertabelle fuer PIO
	ld	c,(ix+ltpsd)
	in	a,(c)
	ld	(ix+ltpdc),a	;Status merken
	pop	ix
	pop	bc
	jp	intraf


; (Re-)Initialisierung
; IX auf Steuertabelle

cdpini:
	LD	(ix+ltpst),11h	;ist initial..; Status neu abfragen
	push	ix
	pop	hl
	ld	(cdpcpa),hl	;ix fuer Interrupt merken
	ld	bc,ltpini
	add	hl,bc
	call	portpr
	ld	c,(ix+ltpsd)
	in	a,(c)		;Scheineingabe
	ld	c,(ix+ltpsc)
	inc	c
	ld	a,87h		;Interrupt Status erlauben
	out	(c),a
	ld	(ix+ltpdc),a	;Status auf Fehlerbedingung legen
	xor	a		;NOP ausgeben
	jr	cdpioo		;und dadurch Status anfordern



10)
 IF @l eq 5
	dw	cdpioo
	dw	cdpioi
	dw	cdpios
	db	0		;du