;**************************************************************
;	PIO-Treiber, Version 12.03.87
;**************************************************************

cdpioi	equ	dumi		;keine Zeicheneingabe unterstuetzt

; Einzelzeichenausgabe (A)

cdpioo:	ld	c,(ix+ltpsd)	;datenport
	out	(c),a
	res	3,(ix+ltpst)	;Sender nicht frei, Status neu abfragen
	ret

; Statusabfrage

cdpios:	ld	a,(ix+ltpst)	;Status
	bit	1,a		;senderseitig blockiert?
	jr	nz,cdpsr	;ja, frei rueckmelden
	bit	3,a		;schon mal frei gemeldet?
	jr	nz,cdpsr1	;ja, Bereitsch.abfrage nur einmal moeglich!
	or	a		;initialisiert?
	call	z,cdpini	;nein
	ld	c,(ix+ltpsc)	;Statusport
	in	a,(c)
	bit	1,a		;liegt Status an?
	jr	z,cdpsr		;nein, nicht frei
	ld	c,(ix+ltpsd)	;Datenport
	in	a,(c)		;Status holen, dann empf.bereit
	ld	(ix+ltpdc),a	;Status merken

; Bedeutung der Statusbits PIO-Drucker:
;--------------------------------------
; Bit 0	Kommandofehler
; Bit 1	Hardwarefehler
; Bit 2	Papierende
; Bit 3	Farbbandende
; Bit 4	Rotdruck
; Bit 5	Druckertyp, 0=1152, 1=1157
; Bit 6	Druckbreite, 0=132, 1=210
; Bit 7	Zeichenabstand, 0=variabel, 1=fest

	and	7		;Fehler?
	cpl			;   z-Flag umkehren
	jr	nz,cdpsr	;nein, frei
	ld	c,(ix+ltpsd)
	out	(c),a		;NOP an Drucker (neuer Status)

cdpsr:	set	3,(ix+ltpst)	;frei, Status nicht neu abfragen
cdpsr1:	ld	a,0ffh
	ret	nz		;senderseitig frei
	res	3,(ix+ltpst)	;nicht frei
	inc	a		;a:=00, ret z
	ret			;senderseitig besetzt



; (Re-)Initialisierung
; IX auf Steuertabelle

cdpini:
	LD	(ix+ltpst),11h	;ist initial..; Status neu abfragen
	LD	(ix+ltpdc),11H	;DC1 nach Initialisierung simulieren
	push	ix
	pop	hl
	ld	bc,ltpini
	add	hl,bc
	call	portpr
	ld	de,0
cdpiz1:	dec	de
	ld	c,(ix+ltpsd)
	in	a,(c)		;alten status wegschm.
	xor	a		;nop
	out	(c),a		;ausgeben fuer neuen status
	call	cdpios		;geraet bereit?
	jr	nz,cdpiz2	;ja
	ld	a,d
	or	e		;de=0, d.h. nicht bereit?
	jr	nz,cdpiz1	;nein, weiter versuchen
cdpiz2:
	ret


tenport byte e/a bidirektional
	db	2+1
	db	@ls+1