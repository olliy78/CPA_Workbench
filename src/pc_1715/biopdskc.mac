
; Kaltstart-Initialisierung Floppy
;=================================
; Version 28.1.88

;; Port Steuer-B
	ld	hl,diomrk	;; Adressmarken-Interrupt
	ld	(intvec+ivdsk2),hl
	ld	a,ivdsk2
	out	(flcobc),a
	ld	a,0cfh		;; bit E/A
	out	(flcobc),a
 IF fdc eq f1715
	ld	a,0e3h		;; eeea aaee
	out	(flcobc),a
	ld	a,00110111b	;; di/oder/high/Maske
	out	(flcobc),a
 ENDIF
 IF fdc eq k5120
        ld      a,0f7h          ;; eeee aeee     
	out	(flcobc),a
	ld	a,00010111b	;; di/oder/LOW /Maske
	out	(flcobc),a
 ENDIF
	ld	a,11111101b	;; Interrupt bei Marke
	out	(flcobc),a
;; Port Daten-B
	ld	a,4fh		;; byte-Eingabe
	out	(fldabc),a
;; Port Daten-A
	ld	a,0fh		;; byte-Ausgabe
	out	(fldaac),a
;; Port Steuer-A
	out	(flcoac),a	;; byte-Ausgabe
	out	(flcoad),a	;; AMF aus
	ld	hl,itimeo
	ld	(intvec+ivdsk1),hl
	ld	a,ivdsk1
	out	(flcoac),a	;; Index-Interrupt
	ld	a,3
	out	(flcoac),a	;; verbieten

;; Select-Latch-Programmierung
	ld	a,0ffh		;; alle LW ausschalten
	out	(flsel),a
 IF fdc eq f1715
	out	(flmot),a
 ENDIF

;; Ermittlung der vorhandenen Laufwerke
	ld	c,maxlw
	ld	d,0
	ld	a,0f7h		;; ohne lock
fl.ka1: rlca
	out	(flsel),a
 IF fdc eq f1715
	out	(flmot),a
 ENDIF
 	ex	af,af'
	ld	b,85		;; max 85 mal steppen
fl.ka2:	in	a,(flcobd)
	rlca			;; kam Spur0-Signal?
	jr	nc,fl.ka4	;; -> ja
	ld	a,01011111b	;; steppen in Richtung Spur 0
	out	(flcoad),a
	ld	a,11011111b	;; mit Kopf oben
	out	(flcoad),a
	ld	h,8		;; Schrittzeit abwarten
fl.ka3: dec	l
	jr	nz,fl.ka3
	dec	h
	jr	nz,fl.ka3
	djnz	fl.ka2		;; -> weiteren Schritt ausfuehren
;****Laufwerke immer existent melden wegen 8" Beistell-Laufwerken ohne Strom
;***	jr	fl.ka5		;; -> LW nicht existent
;***
fl.ka4: set	6,d		;; Laufwerk existent melden
fl.ka5: dec	c		;; weiteres LW?
	jr	z,fl.ka6	;; -> nein
	rrc	d
	rrc	d
	ex	af,af'
	jr	fl.ka1		;; -> naechstes LW pruefen

fl.ka6: ld	a,d
	ld	(lwexis),a

	ld	a,0ffh		;; alle LW ausschalten
	out	(flsel),a
 IF fdc eq f1715
	out	(flmot),a
 ENDIF
	xor	a
	ld	(alwnr),a	;; LW 0 ist das zuletzt benutzte, Motor ist aus
	ld	(aspnr),a	;; steht auf Spur 0
	ld	b,maxlw
	ld	hl,spnrl
fl.ka7: ld	(hl),a		;; alle alten Spurnr. sind 0
	inc	hl
	djnz	fl.ka7

